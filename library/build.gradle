apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    compileSdkVersion COMPILE_SDK_VERSION as int
    buildToolsVersion BUILD_TOOLS_VERSION

    defaultConfig {
        minSdkVersion MIN_SDK_VERSION as int
        targetSdkVersion TARGET_SDK_VERSION as int
        versionCode VERSION_CODE as int
        versionName VERSION_NAME

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
}

dependencies {
    implementation "androidx.appcompat:appcompat:$ANDROIDX_VERSION"
    implementation "androidx.constraintlayout:constraintlayout:1.1.3"
    androidTestImplementation "androidx.annotation:annotation:$ANDROIDX_VERSION"
    androidTestImplementation "androidx.test.ext:junit:$ANDROIDX_EXT_VERSION"
    androidTestImplementation "androidx.test:rules:$ANDROIDX_TEST_VERSION"
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

File repo = new File(rootDir, "repository")

afterEvaluate {
    publishing {
        publications {
            maven(MavenPublication) {
                from components.release

                groupId = GROUP
                artifactId = ARTIFACT_ID
                version = VERSION_NAME
            }
        }
        repositories {
            maven {
                name = 'GitHub'
                url = 'https://maven.pkg.github.com/kyori19/ExpandableLayout'

                credentials {
                    username = properties.getProperty('username')
                    password = properties.getProperty('token')
                }
            }

            maven {
                name = 'Local'
                url = "file://${repo.absolutePath}"
            }
        }
    }
}
